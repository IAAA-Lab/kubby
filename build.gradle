buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$sonar_version"
    }
}

allprojects {
    group = "es.iaaa"
    version = "1.0-SNAPSHOT"

    repositories {
        jcenter()
    }
}

subprojects {

    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'
    apply plugin: 'kotlin'
    apply plugin: 'org.jetbrains.dokka'
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
    test {
        ignoreFailures = true
        jacoco {
            destinationFile = file("${buildDir}/jacoco/test.exec")
        }
    }
    test.finalizedBy(jacocoTestReport)
    jacoco {
        toolVersion = "0.8.3"
    }
    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
        }
    }
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "io.ktor:ktor-server-core:$ktor_version"
        implementation "io.ktor:ktor-server-netty:$ktor_version"
        implementation "io.ktor:ktor-velocity:$ktor_version"
        implementation "org.koin:koin-ktor:$koin_version"
        implementation "com.google.guava:guava:$guava_version"
        implementation "org.apache.jena:apache-jena-libs:$jena_version"
        implementation "ch.qos.logback:logback-classic:$logback_version"

        testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
        testImplementation "org.koin:koin-test:$koin_version"
        testImplementation "io.mockk:mockk:$mockk_version"
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockito_kotlin_version"
        testImplementation "com.jayway.jsonpath:json-path-assert:$json_path_assert_version"
    }
}